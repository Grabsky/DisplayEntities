plugins {
    id("java-library")
    id("xyz.jpenilla.run-paper") version "3.0.2"
    id("de.eldoria.plugin-yml.paper") version "0.8.0"
}

final String VERSION = "1.6.0"
final String RUN_NUMBER = System.getenv("GITHUB_RUN_NUMBER") ?: "DEV"

group = "cloud.grabsky"
version = "$VERSION+$RUN_NUMBER"

repositories {
    mavenCentral()
    // Repository for the Paper API.
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    // Repository for the PacketEvents API.
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    // Repository for the PlaceholderAPI, API.
    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
    // Paper API
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    // Plugin libraries and other runtime dependencies. Downloading and loading handled by Paper's Plugin Loader API.
    paperLibrary("io.github.revxrsal:lamp.common:4.0.0-rc.14")
    paperLibrary("io.github.revxrsal:lamp.bukkit:4.0.0-rc.14")
    paperLibrary("io.github.revxrsal:spec:1.5")
    // Plugin runtime dependencies.
    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("com.github.retrooper:packetevents-spigot:2.11.1")
}

// Registering runFolia task so that Folia compatibility can be tested more easily.
runPaper.folia.registerTask()

tasks {
    runServer {
        minecraftVersion("1.21.11")
        downloadPlugins {
            // modrinth("axiom-paper-plugin", "5.0.2+1.21.10")
            modrinth("luckperms", "v5.5.17-bukkit")
            modrinth("placeholderapi", "2.11.7")
            modrinth("packetevents", "2.11.1+spigot")
        }
    }
    runFolia {
        minecraftVersion("1.21.8")
        downloadPlugins {
            modrinth("luckperms", "v5.5.17-bukkit")
            modrinth("placeholderapi", "2.11.7")
            modrinth("packetevents", "2.11.1")
        }
    }
    compileJava {
        options.fork = true
        options.compilerArgs += "-parameters"
        // Setting compatibility to Java 21 and higher.
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

paper {
    main = "cloud.grabsky.displayentities.DisplayEntities"
    loader = "cloud.grabsky.displayentities.DisplayEntities\$PluginLoader"
    apiVersion = "1.21.10"
    foliaSupported = true
    generateLibrariesJson = true
    serverDependencies {
        "PlaceholderAPI" { load = "BEFORE"; required = false }
        "packetevents"   { load = "BEFORE"; required = false }
    }
}

// Configuring runServer task to use JetBrains' JDK 21 for expanded hot-swap features.
tasks.withType(xyz.jpenilla.runtask.task.AbstractRun.class).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        vendor = JvmVendorSpec.JETBRAINS
        languageVersion = JavaLanguageVersion.of(21)
    }
    jvmArgs("-Xmx1G", "-XX:+AllowEnhancedClassRedefinition", "-Dcom.mojang.eula.agree=true", "-Dnet.kyori.ansi.colorLevel=truecolor")
}

// Returns formatted release name. (for GitHub Actions)
tasks.register("getRelease", () -> print("$VERSION #$RUN_NUMBER"))

// Returns formatted tag name. (for GitHub Actions)
tasks.register("getTag", () -> print("$VERSION+$RUN_NUMBER"))